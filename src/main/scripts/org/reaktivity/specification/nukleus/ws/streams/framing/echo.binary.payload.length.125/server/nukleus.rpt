#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newTargetInitialRef ${nuklei:newReferenceId()} # external scope
property newSourceReplyRef ${nuklei:newReferenceId()} # external scope

property nuklei ${nuklei:directory("target/nukleus-itests").streamsCapacity(1024 * 1024, 64 * 1024)}

property sourceInitial ${nuklei.streams("ws", "source")}
property targetInitial ${nuklei.streams("target", "ws#source")}
property targetReply ${nuklei.streams("ws", "replySource")}
property sourceReply ${nuklei.streams("replyTarget", "ws#replySource")}

property newTargetInitialId ${nuklei:newStreamId()}
property newSourceReplyId ${nuklei:newStreamId()}
property newTargetCorrelationId ${nuklei:newCorrelationId()}

connect await ROUTED_INITIAL
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceInitial.buffer)}
        option writer ${agrona:oneToOneWriter(sourceInitial.throttle)}
connected

read [0x00 0x00 0x00 0x01]
read ([0..8]:initialId)
read ([0..8]:initialRef)
read ([0..8]:correlationId)
read [0xaa]
read [0x00] [0x07] ":scheme" [0x04] "http"
read [0x00] [0x07] ":method" [0x03] "GET"
read [0x00] [0x05] ":path" [0x05] "/echo"
read [0x00] [0x04] "host" [0x0e] "localhost:8080"
read [0x00] [0x07] "upgrade" [0x09] "websocket"
read [0x00] [0x0a] "connection" [0x07] "upgrade"
read [0x00] [0x11] "sec-websocket-key" [0x18] /(?<key>[a-zA-Z0-9+\/=]{24})/
read [0x00] [0x15] "sec-websocket-version" [0x02] "13"

read notify HTTP_BEGIN_INITIAL_RECEIVED

write [0x40 0x00 0x00 0x02]
write ${initialId}
write [0x00 0x02 0x00 0x00]
write flush

read [0x00 0x00 0x00 0x02]
read ${initialId}
read [0x83]
read [0x82 0xfd] ([0..4] :readMask)
read option mask ${readMask}
read ([0..125] :nukleus125)
read option mask [0x00 0x00 0x00 0x00]
read [0x00]

read notify HTTP_DATA_INITIAL_RECEIVED

write [0x40 0x00 0x00 0x02]
write ${initialId}
write [0x04 0x00 0x00 0x00]
write flush

close
closed

connect await ROUTED_INITIAL
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(targetInitial.throttle)}
        option writer ${agrona:oneToOneWriter(targetInitial.buffer)}
connected

write await HTTP_BEGIN_INITIAL_RECEIVED

write [0x00 0x00 0x00 0x01]
write ${newTargetInitialId}
write ${newTargetInitialRef}
write ${newTargetCorrelationId}
write [0x00]
write flush

read [0x40 0x00 0x00 0x02]
read ${newTargetInitialId}
read [0..4]

write await HTTP_DATA_INITIAL_RECEIVED

write [0x00 0x00 0x00 0x02]
write ${newTargetInitialId}
write [0x7d] ${nukleus125}
write [0x01]
write [0x82]
write flush

read [0x40 0x00 0x00 0x02]
read ${newTargetInitialId}
read [0..4]

close
closed

connect await ROUTED_REPLY
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(targetReply.buffer)}
        option writer ${agrona:oneToOneWriter(targetReply.throttle)}
connected

read [0x00 0x00 0x00 0x01]
read ([0..8]:targetReplyId)
read ([0..8]:targetReplyRef)
read ${newTargetCorrelationId}
read [0x00]

write [0x40 0x00 0x00 0x02]
write ${targetReplyId}
write [0x00 0x02 0x00 0x00]
write flush

write notify WS_BEGIN_REPLY_RECEIVED

read [0x00 0x00 0x00 0x02]
read ${targetReplyId}
read [0x7d] ${nukleus125}
read [0x01]
read [0x82]

write [0x40 0x00 0x00 0x02]
write ${targetReplyId}
write [0x08 0x00 0x00 0x00]
write flush

write notify WS_DATA_REPLY_RECEIVED

close
closed

connect await ROUTED_REPLY
        agrona://stream/bidirectional
        option reader ${agrona:oneToOneReader(sourceReply.throttle)}
        option writer ${agrona:oneToOneWriter(sourceReply.buffer)}
connected

write await WS_BEGIN_REPLY_RECEIVED

write [0x00 0x00 0x00 0x01]
write ${newSourceReplyId}
write ${newSourceReplyRef}
write ${correlationId}
write [0x67]
write [0x00] [0x07] ":status" [0x03] "101"
write [0x00] [0x07] "upgrade" [0x09] "websocket"
write [0x00] [0x0a] "connection" [0x07] "upgrade"
write [0x00] [0x14] "sec-websocket-accept" [0x1c] ${ws:handshakeHash(key)}
write flush

read [0x40 0x00 0x00 0x02]
read ${newSourceReplyId}
read [0..4]

write await WS_DATA_REPLY_RECEIVED

write [0x00 0x00 0x00 0x02]
write ${newSourceReplyId}
write [0x7f]
write [0x82 0x7d] ${nukleus125}
write [0x00]
write flush

read [0x40 0x00 0x00 0x02]
read ${newSourceReplyId}
read [0..4]

close
closed
